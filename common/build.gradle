plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.0'
}

apply plugin: 'com.gradleup.shadow'

version = rootProject.version
group = rootProject.group

base {
    archivesName = rootProject.archives_base_name
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.1"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect"
    compileOnly "com.google.code.gson:gson:2.11.0"

    shadow "org.java-websocket:Java-WebSocket:1.5.6"
}

def targetJavaVersion = 8
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(targetJavaVersion)
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

kotlin {
    jvmToolchain(targetJavaVersion)
}

shadowJar {
    configurations = [project.configurations.shadow]

    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude "kotlin/**", "kotlinx/**", "META-INF"
    exclude "org/slf4j/**"
    relocate "org.java_websocket", "cn.disy920.websocket"
}

build {
    dependsOn(shadowJar)
}

jar {

}