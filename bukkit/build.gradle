plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.0'
}

apply plugin: 'com.gradleup.shadow'

version = rootProject.version
group = rootProject.group

base {
    archivesName = rootProject.archives_base_name
}

processResources {
    filesMatching('plugin.yml') {
        expand 'version': project.version
    }
}

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://maven.fastmirror.net/repositories/minecraft/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT"

    shadow "org.java-websocket:Java-WebSocket:1.5.6"
    shadow project(":common")
}

def targetJavaVersion = 8
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(targetJavaVersion)
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

kotlin {
    jvmToolchain(targetJavaVersion)
}

shadowJar {
    configurations = [project.configurations.shadow]

    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude "kotlin/**", "kotlinx/**", "META-INF"
    exclude "org/slf4j/**"

    destinationDirectory = file("${rootProject.projectDir}/build/libs")
    archiveFileName = "${rootProject.archives_base_name}-${project.version}-${project.name}.jar"
}

build {
    dependsOn(shadowJar)
}